import org.eclipse.jgit.api.Git

group 'com.michaeljperri.flutter_sequencer'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.5.21'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.eclipse.jgit:org.eclipse.jgit:5.12.0+"
    }
}

rootProject.allprojects {
    repositories {
        google()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 29

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 16

        externalNativeBuild {
            cmake {
                cppFlags '-std=c++17', '-frelaxed-template-template-args'
            }
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    ndkVersion "22.1.7171670"
    externalNativeBuild {
        cmake {
            version "3.18.1"
            path "CMakeLists.txt"
        }
    }
}

void cloneThirdPartyRepo(String name, String uri, String commit) {
    def dirPath = "$projectDir/third_party/$name"
    def dir = new File(dirPath)

    if (!dir.exists()) {
        dir.mkdirs()
        def repo =
            Git.cloneRepository()
                .setDirectory(dir)
                .setURI(uri)
                .setRemote('origin')
                .setCloneSubmodules(true)
                .call()

        repo.checkout().setName(commit).call()
        repo.submoduleUpdate()
        repo.submoduleInit()
    }
}

task cloneThirdPartyRepos {
    cloneThirdPartyRepo('oboe', 'https://github.com/google/oboe', '06ec23e4f6bc00ba7eea9b84e299f9200a598838')
    cloneThirdPartyRepo('TinySoundFont', 'https://github.com/schellingb/TinySoundFont.git', 'bf574519e601202c3a9d27a74f345921277eed39')
    cloneThirdPartyRepo('sfizz', 'https://github.com/FlowMachinesStudio/sfizz.git', 'ab79889b13bdb97b67f82b88cd37ffea310ba88a')
}
preBuild.dependsOn cloneThirdPartyRepos

task updateSfizzSubmodule(type: Exec) {
    executable "sh"
    args "-c", "git -C third_party/sfizz/external/abseil-cpp checkout refs/tags/20220623.0"
    args "-c", "git -C third_party/sfizz/external/filesystem checkout refs/tags/v1.1.2"
    args "-c", "git -C third_party/sfizz/external/simde checkout refs/tags/v0.7.2"
    args "-c", "git -C third_party/sfizz/external/st_audiofile/thirdparty/dr_libs checkout cac1785cee4abb455817b43d5dee33b49d61be2f"
    args "-c", "git -C third_party/sfizz/external/st_audiofile/thirdparty/libaiff checkout 78864a4a2e769e426be8cfd78ae7f5f72e236c33"
    args "-c", "git -C third_party/sfizz/external/st_audiofile/thirdparty/stb_vorbis checkout fc0bd698b26888da0a632da33f4c49b90763e69b"
    args "-c", "git -C third_party/sfizz/external/vstgui4 checkout 2cf61f5e1fefe4dcd3d19119ddf7b182719a2a5b"
    args "-c", "git -C third_party/sfizz/external/VST_SDK/VST3_SDK/base checkout 985fe019276ee03c2751a1736ba3b390678e29f2"
    args "-c", "git -C third_party/sfizz/external/VST_SDK/VST3_SDK/pluginterfaces checkout b8566ef3b2a0cba60a96e3ef2001224c865c8b36"
    args "-c", "git -C third_party/sfizz/external/VST_SDK/VST3_SDK/public.sdk checkout d69d011fa08c3977928f7da0010f8938f93fd370"
    args "-c", "git -C third_party/sfizz/external/sfzt_auwrapper checkout 014311ae45b86571e1ae3aaa03ebbd7db8b3a32e"
}
preBuild.dependsOn updateSfizzSubmodule

task copyStbVorbis(type: Copy) {
    from 'third_party/sfizz/external/st_audiofile/thirdparty/dr_libs/tests/external/miniaudio/extras/stb_vorbis.c'
    into 'third_party/sfizz/external/st_audiofile/src'
}
preBuild.dependsOn copyStbVorbis

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
